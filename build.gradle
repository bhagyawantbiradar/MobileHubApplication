buildscript {
    ext.buildConfig = [
        'compileSdk': 29,
        'minSdk': 26,
        'targetSdk': 29,

        'version': [
            'major': 1,
            'minor': 0,
            'patch': 0,
            'build': 0,
        ],
    ]

    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext.versions = [
        'kotlin': '1.3.61',
        'ktlint': '0.36.0',
    ]

    ext.deps = [
        'kotlin': [
            'stdlib': [
                'common': "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin}",
                'jdk': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
            ],
        ],
        'androidx': [
            'appCompat': 'androidx.appcompat:appcompat:1.2.0-alpha01',
            'constraintLayout': 'androidx.constraintlayout:constraintlayout:2.0.0-beta4',
            'core': 'androidx.core:core-ktx:1.2.0-rc01',
        ],
        'junit': 'junit:junit:4.13-beta-3',
        'bugsnag': 'com.bugsnag:bugsnag-android:4.21.1',
    ]

    repositories {
        google()
        jcenter()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.1.1'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:1.0.2'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.27.0"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.26.1"
    }
}

allprojects {
    repositories {
        google()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        jcenter()
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: 'com.diffplug.gradle.spotless'

    ktlint {
        version = versions.ktlint
        debug = true
    }

    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('spotless/copyright.kt')
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def propOrEmpty(String name) {
    return hasProperty(name) ? getProperty(name) : ''
}

apply plugin: "com.github.ben-manes.versions"
